export {sort, sorted, quicksort, bubble_sort, is_sorted}

fn bubble_sort(xs) {
    # Perform an in-place bubble sort
    var n = len(xs)
    var next_n = 0
    while n > 1 {
        next_n = 0
        for i in [1..n-1] {
            if xs[i-1] > xs[i] {
                let x = xs[i-1]
                xs[i-1] = xs[i]
                xs[i] = x
                next_n = i
            }
        }
        n = next_n
    }
}

fn is_sorted(xs) {
    let iterator = iter(xs)
    if iterator() is Just(x) {
        var last = x
        while iterator() is Just(x) {
            if x < last {
                return False
            } else {
                last = x
            }
        }
    }
    return True
}

fn quicksort(xs) () {
    quicksort_core(xs, 0, len(xs)-1)
}

fn quicksort_core(a, lo, hi) {
    if lo < hi {
        let p = partition(a, lo, hi)
        quicksort_core(a, lo, p - 1)
        quicksort_core(a, p + 1, hi)
    }
}

fn partition(rg, lo, hi) {
    let pivot = rg[hi]
    var i = lo
    for j in [lo..hi-1] {
        if rg[j] < pivot {
            let ai = rg[i]
            rg[i] = rg[j]
            rg[j] = ai
            i += 1
        }
    }
    let ai = rg[i]
    rg[i] = rg[hi]
    rg[hi] = ai
    return i
}

let sort = quicksort

fn sorted(xs) [a] {
  let ys = vector(xs)
  sort(ys)
  return ys
}

