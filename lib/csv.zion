import itertools {zip}
import parser {ParseState, Span, Step, char, not_char, choice, until_one_of, sequence,
               many, lift, span_concat, text}

export {read_csv_dicts, HasHeaderRow, UseFieldNames}

fn split_csvs(line String) [String] {
  if parse_csv_line(ParseState(line, 0)) is Just(Step(fields, _)) {
    return fields
  }
  return []
}

data ReadCSVDicts {
  HasHeaderRow
  UseFieldNames([String])
}

fn convert_quote(span Span) Span {
  let Span(q, i) = span
  return Span("\"", i)
}

fn parse_csv_line(ps ParseState) Maybe (Step [String]) {
  let escaped_quote = lift(convert_quote, text("\\\""))
  let term_parser = lift(
    strip,
    choice([
      lift(|x| => x[1],
        sequence([
          char('"'),
          lift(span_concat, many(choice([escaped_quote, not_char('"')]))),
          char('"'),
        ])),
      until_one_of(",")
    ]))

  let comma = char(',')
  let parse_terms = lift(
    |x| => fmap(str, flatten(x)),
    sequence([
      many(
        lift(|x| => x[0], sequence([term_parser, comma]))),
      lift(|x| => [x], term_parser),
    ]))

  return parse_terms(ps)
}

fn read_csv_dicts(stream, read_csv_dicts ReadCSVDicts) fn () Maybe (Map String String) {
  let linereader = iter(readlines(stream))
  var headers = []
  var have_headers = False

  return fn () {
    if not have_headers {
      have_headers = True
      match read_csv_dicts {
        HasHeaderRow {
          if linereader() is Just(line) {
            headers = split_csvs(line)
          } else {
            return Nothing
          }
        }
        UseFieldNames(given_headers) {
          headers = vector(given_headers)
        }
      }
    }

    if linereader() is Just(line) {
      return Just({key: value
                   for (key, value) in zip(headers,
                                           split_csvs(line))})
    } else {
      return Nothing
    }
  }
}
